#!../malang.py
"
Copyright (C) 2014 Mattias Ugelvik

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
".

GuessingGame := [
  {Lives, Answer} := @.

  Guess := Io:Input "Guess a number between 0 (inclusive) and 100 (exclusive): ".
  case catch Strings:ToNumber Guess of
    {error, _} -> Io:PrintLn "You need to guess a number, try again.",
                  GuessingGame {Lives, Answer}.
    {no_error, Num} ->
      if Num = Answer
        then Io:Fprint {"You won! The correct answer was indeed {1}.", Answer}.
        else IncorrectGuess {Lives, Answer, Num}.
      end.
  end.
].

IncorrectGuess := [
  {Lives, Answer, Guess} := @.

  if Lives = 0
    then Io:Fprint {"You lose! The correct answer was {1}... you loser.", Answer}.
    else
      HigherOrLower := if Answer > Guess then "higher". else "lower". end,
      LifeOrLives   := if Lives = 1 then "life". else "lives". end,
      Io:Fprint {"You need to guess {1}! You have {2} {3} left.", HigherOrLower, Lives, LifeOrLives}.
      GuessingGame {Lives-1, Answer}.
  end.
].

YesOrNo := [
  Reply := Io:Finput {"{1} [yes/no]: ", @}.
  
  case Reply of
    "yes" -> yeah.
    "no"  -> nope.
    _     -> Io:PrintLn "I don't know what that means. say \"yes\" or \"no\", please.",
             YesOrNo @.
  end.
].

Main := [
  GuessingGame {3, Numbers:RandRange 0 100}.
  if YesOrNo "Play again?"
    then Main {}.
    else ok.
  end.
].


Main {}.
Io:PrintLn "Bye!".
