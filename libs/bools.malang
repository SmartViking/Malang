"""
Copyright (C) 2014 Mattias Ugelvik

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
""".

Truthy := [
  """
  @ = Val

  Returns `true` if `Val` is 'truthy', `false` otherwise.

  These are the 'falsy' values: `{}`, `0`, `false`, `""` and `nil`.
  All other values are considered 'truthy'.
  """.
  case @ of
    {}    -> false.
    0     -> false.
    false -> false.
    ""    -> false.
    nil   -> false.
    _     -> true.
  end.
].


And := [
  """
  @ = {Val1, Val2}

  Returns `true` if and only if both `Val1` and `Val2` are truthy, `false` otherwise.
  """.
  {A1, A2} := @.
  case {Truthy A1, Truthy A2} of
    {true, true} -> true.
    {_, _}       -> false.
  end.
].

Or := [
  """
  @ = {Val1, Val2}

  Returns `true` if either `Val1` or `Val2` is truthy, `false` otherwise.
  """.
  {A1, A2} := @.
  case {Truthy A1, Truthy A2} of
    {false, false} -> false.
    {_, _}         -> true.
  end.
].

Xor := [
  """
  @ = {Val1, Val2}

  Returns `true` if exactly one of `Val1` and `Val2` is truthy, `false` otherwise.
  """.
  {A1, A2} := @.
  case {Truthy A1, Truthy A2} of
    {Same, Same} -> false.
    {_, _}       -> true.
  end.
].

Not := [
  """
  @ = Val

  Returns `false` if `Val` is truthy, `true` otherwise.
  """
  case Truthy @ of
    true  -> false.
    false -> true.
  end.
].
